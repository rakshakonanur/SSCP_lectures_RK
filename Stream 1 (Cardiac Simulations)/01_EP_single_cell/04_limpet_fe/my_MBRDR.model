V; .nodal(); .external(Vm);
Iion; .nodal(); .external();

V; .lookup(-800, 800, 0.05);
Ca_i; .lookup(0.001, 30, 0.001);
.units(uM);

m_init= 0.00255455;
h_init=       0.772909;
j_init=       0.352528;
d_init=       0.00481312;
f_init=       0.898895;
X_init=       0.09;

V_init = -86.926861;

a_m = ((V < 100)
       ? 0.9*(V+42.65)/(1.-exp(-0.22*(V+42.65)))
       : 890.94379*exp(.0486479163*(V-100.))/
            (1.+5.93962526*exp(.0486479163*(V-100.)))
       );

b_m = ((V > -85)
       ? 1.437*exp(-.085*(V+39.75))
       : 100./(1.+.48640816*exp(.2597503577*(V+85.)))
       );

a_h = ((V>-90.)
       ? 0.1*exp(-.193*(V+79.65))
       : .737097507-.1422598189*(V+90.)
       );

b_h = 1.7/(1.+exp(-.095*(V+20.5)));

/*
#ifdef USE_JGATE
    Real a_j   = (0.055*exp(-0.25*(V+78.)))/(exp(-0.2*(V+78.))+1.);
    Real b_j   = 0.3/(exp(-0.1*(V+32.))+1.);
    Real tau_j = 1/(a_j +b_j );
    Real j_inf = a_j/(a_j +b_j);
#endif
*/

APDshorten = 1;

a_d = APDshorten*(0.095*exp(-0.01*(V-5.)))/
  (exp(-0.072*(V-5.))+1.);
b_d = APDshorten*(0.07*exp(-0.017*(V+44.)))/
  (exp(0.05*(V+44.))+1.) ;

a_f = APDshorten*(0.012*exp(-0.008*(V+28.)))/
  (exp(0.15*(V+28.))+1.);
b_f = APDshorten*(0.0065*exp(-0.02*(V+30.)))/
  (exp(-0.2*(V+30.))+1.);

a_X = ((V<400.)
       ? (0.0005*exp(0.083*(V+50.)))/
       (exp(0.057*(V+50.))+1.) 
       : 151.7994692*exp(.06546786198*(V-400.))/
       (1.+1.517994692*exp(.06546786198*(V-400.)))
       );

b_X   = (0.0013*exp(-0.06*(V+20.)))/(exp(-0.04*(V+20.))+1.);

xti   = 0.8*(exp(0.04*(V+77.))-1.)/exp(0.04*(V+35.));

I_K = (( V != -23. )
       ? 0.35*(4.*(exp(0.04*(V+85.))-1.)/(exp(0.08*(V+53.))+
                                              exp(0.04*(V+53.)))-
               0.2*(V+23.)/expm1(-0.04*(V+23.)))
       : 
       0.35*(4.*(exp(0.04*(V+85.))-1.)/(exp(0.08*(V+53.))+
                                            exp(0.04*(V+53.))) + 0.2/0.04 )
       );

Esi = -82.3-13.0287*log(Ca_i/1.e6);

GNa = 15;
ENa  = 40.0;
I_Na = GNa*m*m*m*h*(V-ENa);
//I_Na  *= sv->j;
Gsi = 0.09;
I_si = Gsi*d*f*(V-Esi);
I_X  = X*xti;

Iion= I_Na+I_si+I_X+I_K;

Ca_i_init = 3.e-1;
diff_Ca_i = ((V<200.)
             ? (-1.e-1*I_si+0.07*1.e6*(1.e-7-Ca_i/1.e6))
             : 0
             );

group {
  GNa;
  Gsi;
  APDshorten;
}
.param();

group {
  I_Na;
  I_si;
  I_X;
  I_K;
}
.trace();
